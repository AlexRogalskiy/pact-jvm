plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm'

    id 'groovy'
    id "io.gitlab.arturbosch.detekt"
    id 'codenarc'
}

repositories {
    mavenLocal()
    mavenCentral()
}

version = '4.4.0-beta.8'
group = 'au.com.dius.pact'

java {
    targetCompatibility = '11'
    sourceCompatibility = '11'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
    }
}

dependencies {
    constraints {
        // Define dependency versions as constraints
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'org.apache.commons:commons-text:1.10.0'
        implementation 'io.github.microutils:kotlin-logging:2.0.10'
        implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.1'
        implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.1.1'
        implementation 'com.michael-bull.kotlin-result:kotlin-result:1.1.16'

        testImplementation 'org.apache.groovy:groovy:4.0.6'
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation 'junit:junit:4.13.2'
    }

    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
    testImplementation 'org.spockframework:spock-core'

    codenarc('org.codenarc:CodeNarc:3.1.0-groovy-4.0')
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.9.0')
        }
    }
}

test {
    // Mark sure Pact-JVM builds don't skew the metrics
    systemProperty 'pact_do_not_track', 'true'
}

codenarcMain {
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
}

codenarcTest {
    configFile = rootProject.file('config/codenarc/rulesetTest.groovy')
}

detekt {
    failFast = false
    config = files(rootProject.file("config/detekt-config.yml"))
}
