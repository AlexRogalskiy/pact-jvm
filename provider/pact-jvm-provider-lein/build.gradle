buildscript {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath 'org.apache.commons:commons-lang3:3.10'
  }
}

plugins {
  id "nebula.clojure" version "9.4.0"
}

import org.apache.commons.lang3.RandomStringUtils

dependencies {
  compile project(":provider:pact-jvm-provider")
  compile 'org.clojure:clojure:1.10.1'
  compile 'org.clojure:core.match:0.3.0'
  compile 'leiningen-core:leiningen-core:2.9.3'
  compile 'org.apache.maven:maven-aether-provider:3.0.5'
  compile 'org.sonatype.aether:aether-connector-file:1.13.1'
  compile 'org.sonatype.aether:aether-connector-wagon:1.13.1'
  compile "org.apache.httpcomponents:httpclient:${project.httpClientVersion}"
  compile "org.fusesource.jansi:jansi:${project.jansiVersion}"
  compile "org.codehaus.groovy:groovy:${project.groovyVersion}"

  testRuntime 'org.clojure:tools.nrepl:0.2.13'
  testRuntime "org.junit.vintage:junit-vintage-engine:${project.junit5Version}"
  testCompile('org.spockframework:spock-core:2.0-M2-groovy-3.0') {
    exclude group: 'org.codehaus.groovy'
  }

  groovyDoc "org.codehaus.groovy:groovy-all:${project.groovyVersion}"
}

clojure.aotCompile = true
clojureTest.junit = true
clojureRepl.port = '7888'

compileClojure {
  dependsOn compileGroovy
  classpath = classpath.plus(files(compileGroovy.destinationDir))
  destinationDir = file("${project.buildDir}/classes/java/main")
}

clojureTest {
  classpath = classpath.plus(files(compileGroovy.destinationDir))
  junitOutputDir = file("$buildDir/test-results/clojure/" + RandomStringUtils.randomAlphanumeric(6))
}

processResources {
  expand project.properties
}
