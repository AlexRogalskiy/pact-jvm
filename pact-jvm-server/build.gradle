buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath 'org.apache.commons:commons-text:1.5'
  }
}

plugins {
  id 'scala'
  id 'application'
  id 'maven-publish'
}

mainClassName = 'au.com.dius.pact.server.Server'

dependencies {
  implementation project(":consumer")
  implementation project(":core:pactbroker")
  implementation "ch.qos.logback:logback-core:${project.logbackVersion}",
    "ch.qos.logback:logback-classic:${project.logbackVersion}",
    "com.github.scopt:scopt_2.12:3.5.0"
  implementation("com.typesafe.scala-logging:scala-logging_2.12:3.7.2") {
      exclude group: 'org.scala-lang'
  }
  implementation 'ws.unfiltered:unfiltered-netty-server_2.12:0.10.4'
  implementation 'org.apache.commons:commons-io:1.3.2'
  implementation "org.apache.tika:tika-core:${project.tika}"
  implementation "org.apache.commons:commons-lang3:${project.commonsLang3Version}"

  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${project.junit5Version}"
  testImplementation "org.apache.groovy:groovy:${project.groovyVersion}"
  testImplementation("org.spockframework:spock-core:${project.spock}") {
    exclude group: 'org.codehaus.groovy'
  }
  testRuntimeOnly "net.bytebuddy:byte-buddy:${project.bytebuddyVersion}"
}

jar {
    manifest.attributes 'Main-Class': mainClassName,
            'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' ')
}

import org.apache.commons.text.StringEscapeUtils
publishing {
  publications {
    serverDistribution(MavenPublication) {
      from components.java
      artifact distZip
      artifact distTar
      pom {
        name = project.name
        packaging = 'jar'
        description = StringEscapeUtils.escapeXml11(new File(projectDir, 'README.md').text)
        url = 'https://github.com/DiUS/pact-jvm'
        licenses {
          license {
            name = 'Apache 2'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }
        scm {
          url = 'https://github.com/DiUS/pact-jvm'
          connection = 'https://github.com/DiUS/pact-jvm.git'
        }

        developers {
          developer {
            id = 'thetrav'
            name = 'Travis Dixon'
            email = 'the.trav@gmail.com'
          }
          developer {
            id = 'rholshausen'
            name = 'Ronald Holshausen'
            email = 'rholshausen@dius.com.au'
          }
        }
      }
    }
  }
}

signing {
  required { project.hasProperty('isRelease') }
  sign publishing.publications.serverDistribution
}
