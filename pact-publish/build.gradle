buildscript {
  dependencies {
    classpath "au.com.dius.pact.provider:gradle:4.1.7"
  }
}

if (System.env.PACT_PUBLISH == 'true') {
  apply plugin: 'au.com.dius.pact'
}

dependencies {
  testImplementation project(":core:pactbroker")
  testImplementation("org.codehaus.groovy:groovy:${project.groovyVersion}")
  testImplementation "org.codehaus.groovy:groovy-json:${project.groovyVersion}"
  testImplementation(project(":consumer:groovy")) {
    transitive = false
  }
  testImplementation(project(":consumer"))
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${project.junit5Version}"
  testRuntimeOnly "ch.qos.logback:logback-classic:${project.logbackVersion}"
  testImplementation("org.spockframework:spock-core:${project.spock}") {
    exclude group: 'org.codehaus.groovy'
  }
}

project.afterEvaluate {
  project.tasks.publishMavenPublicationPublicationToMavenLocal.enabled = false
  project.tasks.publishMavenPublicationPublicationToMavenRepository.enabled = false
}

if (System.env.PACT_PUBLISH == 'true') {
  pact {
    publish {
      pactBrokerUrl = 'https://pact-foundation.pactflow.io'
      if (project.hasProperty('pactBrokerToken')) {
        pactBrokerToken = project.pactBrokerToken
      }
      excludes = ['JVM Pact Broker Client-Imaginary Pact Broker']
    }
  }
}
